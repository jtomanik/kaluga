println "tasks: $gradle.startParameter.taskNames"

enableFeaturePreview("GRADLE_METADATA")
// set some global variables
def gradleProps = new Properties()
def gradlePropfile = file("../gradle.properties")
gradlePropfile.withInputStream { gradleProps.load(it) }


gradle.ext {
    kotlin_version = gradleProps.kotlin_version // comes from gradle.properties in the root of kaluga project
    // and the settings.gradle in the example folder
    kotlinx_corountines_version = '1.3.0'
    library_version = '0.0.3'
    android_version = 27
    android_target_version = 29
    android_build_version = gradleProps.android_build_version // comes from gradle.properties in the root of kaluga project
}

// load some more from local.properties or set defaults.
def props = new Properties()
def propfile = file("../local.properties")
if (propfile.exists()) {
    propfile.withInputStream { props.load(it) }
    gradle.ext.exampleAsRoot = Boolean.parseBoolean(props["exampleAsRoot"])
    println "local.properties read (exampleAsRoot=${props['exampleAsRoot']}, using $gradle.ext.exampleAsRoot)"
} else {
    gradle.ext.exampleAsRoot = false
    println "local.properties not found, using default vaxlues (exampleAsRoot=$gradle.ext.exampleAsRoot)"
}

// based on https://github.com/Kotlin/xcode-compat/blob/d677a43edc46c50888bca0a7890a81f976a42809/xcode-compat/src/main/kotlin/org/jetbrains/kotlin/xcodecompat/XcodeCompatPlugin.kt#L16
def sdkName = System.getenv("SDK_NAME") ?: "unknown"
if (sdkName.startsWith("iphonesimulator"))
    gradle.ext.ios_arch = "iosX64"
else if (sdkName.startsWith("iphoneos"))
    gradle.ext.ios_arch =  "iosArm64"
else
    gradle.ext.ios_arch = "iosX64"

println("Detected arch from sdk $sdkName: $gradle.ext.ios_arch")

// set global variable to decide how many iOS source sets to use. More than one sourceset can create problems for project dependencies and gives false errors in the IDE
// currently the xcodecompat plugin also supports only arm64/iosX64 so this is off by default
// it can be enabled when publishing, in order to generate multiple artifacts
def envFlag = System.env.IOS_ONE_SOURCESET
gradle.ext.ios_one_sourceset = envFlag == null || Boolean.parseBoolean(envFlag ?: "true")
println "Using a single iOS sourceset (IOS_ONE_SOURCESET=$envFlag): $gradle.ext.ios_one_sourceset"
gradle.ext.ios_suffix_archs = gradle.ext.ios_one_sourceset ? [""] : ["X64", "Arm64", "Arm32"]

println "iOS architecture suffix collection: $gradle.ext.ios_suffix_archs"
